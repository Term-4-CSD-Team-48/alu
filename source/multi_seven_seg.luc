module multi_seven_seg #(
    DIGITS = 4 : DIGITS > 0,
    DIV = 64 : DIV >= 0
)(
    input clk,                // clock
    input rst,                // reset
    input values[DIGITS][4],  // values to show
    output seg[7],            // LED segments
    output sel[DIGITS]        // Digit select
) {
    
    // number of bits required to store DIGITS-1
    const DIGIT_BITS = $clog2(DIGITS)
    
    .clk(clk), .rst(rst) {
        counter ctr (#DIV(DIV), #SIZE(DIGIT_BITS), #TOP(DIGITS-1)) 
    }
    
    seven_segment_encoder enc
    decoder digit_dec(#WIDTH(DIGIT_BITS))
    
    always {
        enc.char = values[ctr.value]
        seg = enc.segs
        
        digit_dec.in = ctr.value 
        sel = digit_dec.out
    }
}